#> item:fishing/rod/used
# é‡£ã‚Šç«¿ã‚’ä½¿ç”¨ã—ãŸã“ã¨ã®æ¤œçŸ¥

tag @s add UsedRod

# è€ä¹…å€¤ã‚’æ¸›ã‚‰ã™
    execute if items entity @s weapon.mainhand fishing_rod run tag @s add MainhandRod
    execute unless entity @s[tag=MainhandRod] if items entity @s weapon.offhand fishing_rod run tag @s add OffhandRod
    function item:tool/durability/_

# é‡£ã£ãŸé­šã«å¯¾ã—ã¦ã®å‡¦ç†
    execute as @e[type=item] if data entity @s Item.components.minecraft:custom_data{item_type:"fishing_loot"} at @s run function item:fishing/_

# å…¨ç¨®é¡é‡£ã£ã¦ã„ã‚‹ã‹æ¤œçŸ¥
    execute unless data storage zukan:fish Data[{count:0}] run advancement grant @a only asset:achievement/2.5
    execute unless data storage zukan:fish Data[{count:0}] run data modify storage global: fish.item_id set value "completed_fish_zukan"
    execute store result score $FishKind Temporary if data storage zukan:fish Data[]
    execute store result score $KingKind Temporary if data storage zukan:fish Data[{crown:"ğŸ¥‡"}]
    execute if score $KingKind Temporary = $FishKind Temporary run advancement grant @a only player:trigger/achievement_0.1
    execute if score $KingKind Temporary = $FishKind Temporary run data modify storage global: fish.item_id set value "perfect_fish_zukan"

# ãƒ¬ãƒ™ãƒ«ãŒä¸ŠãŒã‚‰ãªã„ã‚ˆã†ã«
    kill @e[type=experience_orb,distance=..1]

# ãƒªã‚»ãƒƒãƒˆ
    tag @s remove UsedRod
    tag @s remove MainhandRod
    tag @s remove OffhandRod
    scoreboard players reset $FishKind Temporary
    scoreboard players reset $KingKind Temporary
    advancement revoke @s only player:trigger/used_item/fishing_rod